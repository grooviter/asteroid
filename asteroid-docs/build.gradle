buildscript {
    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.4.2'
    }
}

plugins {
  id 'org.asciidoctor.convert' version '1.5.3'
  id 'com.github.jruby-gradle.base' version '1.2.1'
}

apply plugin: 'org.ajoberstar.github-pages'
apply plugin: 'com.github.jruby-gradle.base'
apply plugin: 'org.asciidoctor.convert'

dependencies {
    gems 'rubygems:asciidoctor-diagram:1.4.0'
}

version = releaseVersion

asciidoctor {
    dependsOn jrubyPrepare

    attributes 'source-highlighter': 'prettify',
               toc                 : 'left',
               icons               : 'font',
               toclevels           : 3,
               'pdf-style'         : 'default'

    requires = ['asciidoctor-diagram']
    gemPath  = jrubyPrepare.outputDir
}

task allDocs(type: Copy) {
    from "../asteroid-core/build/docs/javadoc/"
    into "build/asciidoc/html5/javadocs/"
}

githubPages {
    repoUri = 'https://github.com/grooviter/asteroid.git'
    deleteExistingFiles = true

    pages {
        from file('build/asciidoc/html5')
    }

    //https://github.com/ajoberstar/gradle-git/wiki/Travis-CI-Authentication
    credentials {
        username = project.properties.githubToken
        password = ''
    }
}

publishGhPages.dependsOn 'allDocs'
allDocs.dependsOn 'asciidoctor'
asciidoctor.dependsOn ':asteroid-core:javadoc'
