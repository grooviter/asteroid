buildscript {
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
    }
}

apply plugin: 'pmd'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    compile libs.groovy
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            groupId    releaseGroup
            artifactId 'asteroid'
            version    releaseVersion

            from components.java
        }
    }
}

bintray {
 /* Normally taken from ~/.gradle/gradle.properties */
    user         = bintrayUser
    key          = bintrayKey
    publish      = false
    publications = ['mavenCustom']
    pkg {
        repo            = 'maven'
        name            = 'asteroid'
        userOrg         = 'grooviter'
        desc            =  releaseDesc
        websiteUrl      = 'http://grooviter.github.io/asteroid/'
        issueTrackerUrl = 'https://github.com/grooviter/asteroid/issues'
        licenses        = ['Apache-2.0']
        vcsUrl          = 'https://github.com/grooviter/asteroid.git'
        labels          = ['groovy', 'ast']
        version {
            name      = releaseVersion
            desc      = 'Asteroid Release'
            released  = new Date()
            vcsTag    = releaseVersion
        }
    }
}


javadoc {
    options.stylesheetFile = "$projectDir/src/javadoc/stylesheet.css" as File
    options.windowTitle = 'Asteroid API Documentation'
    options.docTitle = "Asteroid ($releaseVersion)"
}


pmd {
    ruleSets = [
        'java-design',
        'java-empty',
        'java-finalizers',
        'java-imports',
        'java-naming',
        'java-optimizations',
        'java-strictexception',
        'java-strings',
        'java-typeresolution',
        'java-unnecessary',
        'java-unusedcode'
    ]
    ignoreFailures = true
}
